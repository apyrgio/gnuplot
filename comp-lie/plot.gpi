###################
# General options #
###################

set terminal pdf enhanced font "Times"

# General variables
data = "data"
folder = "`echo ${PWD##*/}`"
out = sprintf("%s.pdf", folder)
report(p, t, n) = sprintf("%s/%s/report-bench-%s%d.log", data, p, t, n)
graph_title(p, n) = sprintf("%s-iodepth-%d", p, 4 + (n - 1) * 4)

# Many samples for better quality
set samples 1001

#################
# Graph options #
#################

# only y-axis grid
set grid y

ylabel = "Bandwidth (MB/s)"
xlabel = "Block size (KB)"

# save file as "<current dir>.pdf"
set output "bw-write-".out
set key right top
set yrange [0:400]

load "../broken1.gpi"

plot for [i = 1:4] report("sosd", "write", i) using 5:1:xtic(5) \
	with linespoints title graph_title("sosd", i)

load "../broken2.gpi"

set key right center
set yrange [600:10000]

plot for [i = 1:4] report("cached", "write", i) using 5:1:xtic(5) \
	with linespoints title graph_title("cached", i)

load "../broken3.gpi"

set output "bw-read-".out

set key right top
set yrange [0:795]

load "../broken1.gpi"

plot for [i = 1:4] report("sosd", "read", i) using 5:1:xtic(5) \
	with linespoints title graph_title("sosd", i)

load "../broken2.gpi"

set key right center
set yrange [0:11000]

plot for [i = 1:4] report("cached", "read", i) using 5:1:xtic(5) \
	with linespoints title graph_title("cached", i)

load "../broken3.gpi"

ylabel = "Latency (Î¼s)"
set output "lat-write-".out

load "../broken1.gpi"

set key left top
set yrange [0:110]

plot for [i = 1:4] report("cached", "write", i) using 5:3:xtic(5) \
	with linespoints title graph_title("cached", i)

load "../broken2.gpi"
set key right top
set yrange [3500:6000]

plot for [i = 1:4] report("sosd", "write", i) using 5:3:xtic(5) \
	with linespoints title graph_title("sosd", i)

load "../broken3.gpi"
set output "lat-read-".out

load "../broken1.gpi"

set key left top
set yrange [0:120]

plot for [i = 1:4] report("cached", "read", i) using 5:3:xtic(5) \
	with linespoints title graph_title("cached", i)

load "../broken2.gpi"

set key right top
set yrange [1000:4400]
plot for [i = 1:4] report("sosd", "read", i) using 5:3:xtic(5) \
	with linespoints title graph_title("sosd", i)
